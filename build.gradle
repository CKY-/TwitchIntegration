buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "3.+", changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: "idea"
apply plugin: "maven"

def BUILD_NUMBER = System.getenv("BUILD_NUMBER") ?: patch_version

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

version = major_version + "." + minor_version + "." + BUILD_NUMBER
group = "net.blay09.mods"
archivesBaseName = mod_name + "_" + minecraft_version

repositories {
    maven {
        url 'https://artifactory.blay09.net/artifactory/libs-release'
    }
    maven {
        url 'https://artifactory.blay09.net/artifactory/libs-snapshot'
    }
}

configurations {
    shade
}

dependencies {
    minecraft([
            group  : "${forge_group}",
            name   : "forge",
            version: "${minecraft_version}-${forge_version}"
    ])
    //implementation fg.deobf("net.blay09.mods:ChatTweaks_1.16.2:6.0.0")

    implementation "net.blay09:javairc:0.1.0-SNAPSHOT"
    shade "net.blay09:javairc:0.1.0-SNAPSHOT"

    implementation "net.blay09:javatmi:0.1.0-SNAPSHOT"
    shade "net.blay09:javatmi:0.1.0-SNAPSHOT"
}

minecraft {
    mappings channel: mappings_channel, version: mappings_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                twitchchatintegration {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'

            mods {
                twitchchatintegration {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
//            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/')

            mods {
                twitchchatintegration {
                    source sourceSets.main
                }
            }
        }
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'net.blay09.javairc', 'net.blay09.repack.javairc'
    relocate 'net.blay09.javatmi', 'net.blay09.repack.javatmi'
}

jar {
    manifest {
        attributes(["Specification-Title"     : mod_id,
                    "Specification-Vendor"    : "BlayTheNinth",
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "BlayTheNinth",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

idea {
    module {
        for (String excludeDirName in ["run", "out", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}

reobf {
    shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar
